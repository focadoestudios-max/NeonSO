// biblis/ns.fpb
#incluir "biblis/texs.fpb";

#global ns_abrir();

#global car[64] comando;
#global int conta = 0;
#global int cor = 0xFFFF0000;

car _obter_car();
vazio ns_loop();
vazio mudar_cor();
vazio _att_tela();
vazio _render_retangulo(int x, int y, int largura, int altura, int cor);
vazio _limpar_tela(int cor);

vazio ns_abrir() {
    escrever("[FPB]: teste de FPB funcionando\n");
    escrever("[Neon Script]: abrindo sessão...\n");
    escrever("[Neon Script]: sessão iniciada\n\n")
    escrever(
        "[NEON]: Inicializado com sucesso\n",
        "[Direitos Autorais]: Foca-do Estúdios\n",
        "[Autor]: Shiniga-OP\n\n",
        "Digite \"-ajuda\" para ver todos os comandos!\n\n"
    );
    escrever("~ $ ");
    // inicia o buffer de comando
    por(int i = 0; i < 64; i++) {
        comando[i] = (car)0x00;
    }
    ns_loop();
}

vazio ns_loop() {
    car c = _obter_car();
    
    // pra apagar:
    se(c == 0x08 || c == 0x7F) { 
        se(conta > 0) {
            conta--;
            comando[conta] = '\0';
            // sequencia pra apagar visualmente no terminal:
            // retroceder, espaço(apaga), retroceder
            escrever((car)0x08, ' ', (car)0x08);
        }
    } senao se(c == 0x0D) { // processa a tecla
        escrever('\n');
        se(texcmp(comando, "-status")) {
            escrever("[Kernel]: Neon 0.0.1\n");
            escrever("[Arquitetura]: ARM64\n[Bibliotecas]: Neon Script 0.0.2\n");
            escrever("[Pilha]: 16 KB\n");
        } senao se(texcmp(comando, "-ajuda")) {
            escrever(
                "[Comandos]:\n",
                "-status: status do kernel\n",
                "-cor: muda a cor da tela (azul, vermelho e verde)\n",
                "-render: desenha um retangulo vermelho na tela\n");
        } senao se(texcmp(comando, "-cor")) {
            mudar_cor();
        } senao se(texcmp(comando, "-render")) {
            _render_retangulo(100, 100, 400, 200, 0xFFFF0000);
            _att_tela();
        } senao se(conta > 0) {
            escrever("Erro: \"", comando, "\" não reconhecido\n");
        }
        // reinicia o buffer
        por(int i = 0; i < 64; i++) {
            comando[i] = '\0';
        }
        conta = 0;
        escrever("~ $ ");
    } 
    // caracteres visiveis
    senao se(c >= 0x20 && c <= 0x7E) {
        se(conta < 63) { // proteção pra não estourar o vetor
            escrever(c);
            comando[conta] = c;
            conta++;
        }
    }
    _asm_(
        "ldp x29, x30, [sp, 144]\n",
        "add sp, sp, 160\n",
        "b ns_loop\n"
    );
}

vazio mudar_cor() {
    se(cor == 0xFFFF0000) {
        cor = 0xFF00FF00; // verde
        escrever("[Neon Script]: tela verde\n");
    } senao se(cor == 0xFF00FF00) {
        cor = 0xFF0000FF; // azul
        escrever("[Neon Script]: tela azul\n");
    } senao {
        cor = 0xFFFF0000; // vermelho
        escrever("[Neon Script]: tela vermelha\n");
    }
    _limpar_tela(cor);
    _att_tela();
}